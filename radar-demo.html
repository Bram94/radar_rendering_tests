<!doctype html>
<html>
  <head>
    <title>Radar display test</title>
    <meta name='viewport' content='initial-scale=1,maximum-scale=1,user-scalable=no,width=device-width' />
    <meta name="Description" content="Display radar data using WebGL" />
    <script src="https://code.jquery.com/jquery-3.2.1.min.js"></script>
    <link href="https://api.mapbox.com/mapbox-gl-js/v3.4.0/mapbox-gl.css" rel="stylesheet">
    <script src="https://api.mapbox.com/mapbox-gl-js/v3.4.0/mapbox-gl.js"></script>
    <link rel="stylesheet" href="css/radarstyle.css">
    <link href="https://fonts.googleapis.com/css?family=Oleo+Script&display=swap" rel="stylesheet">
  </head>
  <body>
    <canvas id="texturecolorbar" class="texturecolorbar"></canvas>
    <div id="map" class="mobilemap"></div>
    <script type="x-shader/x-vertex" id="vertexShader"> #version 300 es
      in vec2 aPosition, aTexPosition;
      uniform mat4 u_matrix;
      // Actual provided azimuths length can also be smaller than 720, it just should not be larger;
      uniform float radar_lat, radar_lon, scanangle, gateres, azimuths[720];
      uniform vec2 data_shape;
      int i_az;
      float theta, ke, Re, az, sr, gr, h, x, y, c, cos_c, sin_c, lat, lon, lat_0, lon_0, pi, mx, my;
      out vec2 v_texcoord;
      
      void main() {     
        pi = radians(180.);
        ke = 4./3.;
        Re = 6371000.;
        lat_0 = radians(radar_lat);
        lon_0 = radians(radar_lon);
        theta = radians(scanangle);

        // aPosition.y is an azimuth index, which needs to be mapped to an actual azimuth angle.
        i_az = int(mod(aPosition.y, data_shape.y));
        if (aPosition.y >= data_shape.y) {
          // Vertex buffers are only updated when the number of azimuths or radial gates increases, not when it decreases. So it's possible that not
          // all vertices and associated triangles should be drawn. This is the case when the number of azimuths in the
          // radar scan is smaller than the number on which the vertex buffers were based. In this case any vertex/triangle for which
          // aPosition.y (the azimuth index) >= data_shape.y (number of azimuths), should not be drawn. Setting the azimuth angle equal to that of the
          // first azimuth value realises this, since triangles with 2 or more vertices equal are not drawn.
          
          // For the radial position such a check doesn't seem needed, since for range gates outside the scan's range, v_texcoord (calculated below)
          // will be greater than 1, which is not drawn.
          az = radians(azimuths[0]);
        } else {
          az = radians(azimuths[i_az]);
        }
        // aPosition.x is a range gate index, which needs to be converted to an actual range.
        sr = aPosition.x*gateres;
        h = sqrt(pow(sr, 2.)+pow(ke*Re, 2.)+2.*ke*Re*sr*sin(theta));
        gr = ke*Re*asin(sr*cos(theta)/h);
        
        x = gr*sin(az)/Re;
        y = gr*cos(az)/Re;
        
        c = length(vec2(x, y));
        cos_c = cos(c);
        sin_c = sin(c);

        lat = asin(cos_c*sin(lat_0)+y*sin_c*cos(lat_0)/c);
        lon = lon_0+atan(x*sin_c/(c*cos(lat_0)*cos_c-y*sin(lat_0)*sin_c));
        
        mx = 0.5 + 0.5*lon/pi;
        my = 0.5 - 0.5*log(tan(pi/4. + lat/2.))/pi;
        gl_Position = u_matrix * vec4(mx, my, 0., 1.);
        
        v_texcoord = (aTexPosition+0.5)/data_shape;
      }
      
    </script>
    <script type="x-shader/x-fragment" id="fragmentShader"> #version 300 es
      precision mediump float;
      in vec2 v_texcoord;
      uniform sampler2D u_image;
      uniform sampler2D u_palette;
      float value;
      out vec4 fragColor;

      void main() {
        value = texture(u_image, v_texcoord).a;
        fragColor = texture(u_palette, vec2(value));
      }
    </script>
    <script>
    </script>
  <script type="text/javascript" src="scripts/pako.min.js"></script>
  <script type="text/javascript" src="node_modules/zarr/zarr.umd.js"></script>
  <script type="module" src="scripts/demo-radar-script.js"></script>
  </body>  
</html>


